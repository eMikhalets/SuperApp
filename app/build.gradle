plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-kapt'
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    namespace 'com.emikhalets.superapp'
    compileSdk rootProject.params.compileSdk

    defaultConfig {
        applicationId "com.emikhalets.superapp"
        minSdk rootProject.params.minSdk
        targetSdk rootProject.params.targetSdk
        versionCode rootProject.params.versionCode
        versionName rootProject.params.versionName
        vectorDrawables { useSupportLibrary true }
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPass']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePass']
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility rootProject.params.java
        targetCompatibility rootProject.params.java
    }
    kotlinOptions {
        jvmTarget = rootProject.params.java.toString()
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.4.5'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {

    implementation project(":application:fitness:app")

    implementation project(":core:common")
    implementation project(":core:database")
    implementation project(":core:navigation")
    implementation project(":core:ui")

    implementation 'androidx.core:core-ktx:1.10.1'

    implementation("com.google.dagger:hilt-android:${versions.hilt}")
    kapt("com.google.dagger:hilt-compiler:${versions.hilt}")
}